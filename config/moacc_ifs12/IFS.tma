%{
  /* IFS.tma for cambridgebay_ifs13 */
  #include <stdlib.h> /* for abs() */
  #include <time.h>
  #include "dasio/cmd_writer.h"
  #include "location.h"
  #include "nl.h"

  int DayOfYear(long now) {
    struct tm *times = gmtime(&now);
    return times->tm_yday;
  }

  int16_t DayOfWeek(long now) {
    long dt = (now+(long)(LONGITUDE()*240))/(24L*60*60);
    int16_t DoW = dt%7;
    return DoW;
  }

  /* May be unnecessary */
  int16_t MinutesFromLocalHour(long now, int16_t local_hour) {
    int32_t minutes_since_local_epoch =
      (now/60) + (int32_t)(LONGITUDE())*4;
    int32_t minutes_from_local_hour =
      (minutes_since_local_epoch%(24*60)) - local_hour*60;
    return minutes_from_local_hour;
  }

  const char *Weather_Reason = "Startup";
%}

%{
  /* HercDioB status monitor goes here */
%}

State Init {
      > Telemetry Start
      > _Algo P1 Init
   +1 > Radiance Open Threshold Set 70 percent
      > Radiance Close Threshold Set 60 percent
      > RH Open Threshold Set 88 percent
      > RH Close Threshold Set 90 percent
   +1 Validate Check_Unexpected;
}

State Check_Unexpected "Unexpected.tmas" {
     > _Algo P1 Check_Unexpected
  +1 > Startup Unexpected Set
     Validate Init_Wakeup;
}

State Init_Wakeup "Wakeup.tmas" {
     > _Algo P1 Init_Wakeup
  +1 Validate Init_Choice;
}

State Init_Choice "Startup.tmas" {
     > _Algo P1 Init_Choice
     Hold until ( SWStat == SWS_TIME_WARP ) or 2:00;

## Uncomment IFS commands
# +1 > IFS Time Synch
#    > IFS Set Preamp Gains 3 3
# Don't set InSb preamp gain until after filling.

  +1 Validate Init_Decide;

   # These two will never occur, but are here
   # to improve the visualization
  +1 Validate Holding;
  +1 Validate Failure;
}

State Init_Decide {
#    > IFS Read Status
     > _Algo P1 Init_Decide
  +2 { if ( Convert(Sol_ele) <= 0. ) Validate Init_Close; }
  +2 depending on ( Bad_Weather ) { Validate Nap; }
 +10 Validate Ready_To_Play;
}

State Unexpected_Restart {
  > Startup Unexpected Set
  > Email Error Message Unexpected Restart, Entering Holding
  Validate Holding;
}

State Holding {
      > _Algo P1 Holding
      Validate Dome_Close;
      > Startup Define Validate Holding;
      Hold until valid (Solar_Bedtime);
   +1 > Savelog Shutdown from Holding
      > Startup Wakeup Clear
      Validate Shutdown;
}

State Holding_with_Lamp_Runs {
      > _Algo P1 Holding_with_Lamp_Runs
      Validate Dome_Close;
#     Validate IFS_Status_Update;
      > Startup Define Validate Holding_with_Lamp_Runs;
      Hold until valid (Solar_Bedtime);
   +1 > Savelog Shutdown from Holding_with_Lamp_Runs
      > Startup Wakeup Set
      Validate Shutdown;
}

State Failure {
     > _Algo P1 Failure
#    > IFS Direct SRC=0
#    > IFS Direct VAC=0
  +1 > Enclosure Close
# +1 > IFS IdleScan
#+60 > Power Pump Off
  +1 > Startup Define Validate Failure;
     Hold until valid (Solar_Bedtime);
  +1 > Savelog Shutdown from Failure
     Validate Shutdown;
#  Each Failure Partition sends its own email for help.
}

State Init_Close {
  > _Algo P1 Init_Close
  Validate Dome_Close;
  Validate Sleep;
}

State ReInit {
  > _Algo P1 ReInit
  > Startup Clear
  Validate Watchdog_Okay;
  Validate Init_Choice;
}

State ReInit_and_Pump_Down {
  > _Algo P1 ReInit_and_Pump_Down
  > Startup Clear
  Validate Watchdog_Okay;
  Validate Pump_Down;
}

State Pump_Down {
  > _Algo P1 Pump_Down
# Hold Until ( IFSCStat == IFS_SCAN_IDLE ||
#              IFSCStat == IFS_SCAN_WAIT) OR 4:00;
# Validate IFS_Status_Update;
# Validate Pump_Down_Init;
# Hold until Valid (Pump_Idle);
  Validate Init_Choice;
}

State Sleep {
  depending on ( Watchdog_Error ) { Validate Failure; }
  depending on ( Solar_Bedtime ) { Validate Bedtime; }
  > _Algo P1 Sleep
  > Sun Tracker Sleep
# Validate IFS_Status_Update;
  Hold Until ( Convert(Sol_ele) > 0. );
  Validate Wake_Up;
}

State Wake_Up {
    depending on ( Watchdog_Error ) { Validate Failure; }

    > _Algo P1 Wake_Up
#+1 { if (convert(LN2Depth) < 15)
#       ci_sendfcmd(0, "Email Warning Message LN2 Tank depth is %.1lf\n",
#         convert(LN2Depth));
#   }
#+1 Validate IFS_Status_Update;
#+1 Validate InSb_Fill;
#+1 Validate Pump_Down_Init;
#   Hold until Valid (Pump_Idle);
    > Email Report Message Container has completed Wake_Up sequence.
    depending on ( Good_Weather ) { Validate Ready_To_Play; }
    depending on ( Solar_Bedtime ) { Validate Bedtime; }
}

State Ready_To_Play {
  depending on ( Watchdog_Error ) { Validate Failure; }
  depending on ( Solar_Bedtime ) { Validate Bedtime; }
  depending on ( Bad_Weather) { Validate Nap; }

  > _Algo P1 Ready_To_Play
  Validate Dome_Open;
  > Sun Tracker Track To Programmed Sun Position
  Hold Until Valid ( Dome_Opened ) or 2:00;
  > Sun Tracker Track by CamTracker

  > Email Report Message Enclosure opened in Ready_to_Play

# Hold Until ( (IFSCStat == IFS_SCAN_IDLE ||
#               IFSCStat == IFS_SCAN_WAIT ) &&
#              ST_modus == ST_MODE_TTM );

#   > Email Report Message Scanning
#  #We do this on wakeup at ETL:
#  #depending on (InSb_Startup) { Validate InSb_Fill; }
#+1 Validate IFS_Status_Idle;
#   depending on (InSb_Filled) { Validate Init_Set_InSb_Gains; }
#   depending on (InSb_Chilled) { Validate Play; }
#+1 > Email Warning Message Starting in Play_InGaAs: !InSb_Chilled
#+1 Validate Play_InGaAs;
}

# State Init_Set_InSb_Gains {
#   > _Algo P1 Init_Set_InSb_Gains
# }

# State Play NoLog {
#   > _Algo P1 Play
# }

# State Play_InGaAs NoLog {
#   > _Algo P1 Play_InGaAs
# }

State Scanner_Failure {
   > _Algo P1 Scanner_Failure
   > Email Error Message Scanner failure.
   Validate Failure;
}

# State Scanner_CAN_Err {
#   > _Algo P1 Scanner_CAN_Err
# }

# State Scanner_InSbNR {
#   > _Algo P1 Scanner_InSbNR
# }

State Nap {
  depending on ( Watchdog_Error ) { Validate Failure; }

  { ci_sendfcmd(Cmd_Send,
      "Email Report Message Container has entered Nap State due to %s\n",
      Weather_Reason ); }
  > _Algo P1 Nap
  Validate Dome_Close;
# Hold Until ( IFSCStat == IFS_SCAN_IDLE || IFSCStat == IFS_SCAN_WAIT ) or 4:00;
# Validate IFS_Status_Update;
  depending on ( Good_Weather ) { Validate Ready_To_Play; }
  depending on ( Solar_Bedtime ) { Validate Lights_Out; }
}

State Bedtime {
  depending on (Watchdog_Error ) { Validate Failure; }
  depending on (Bad_Weather) { Validate Nap; }
      > _Algo P1 Bedtime
#     Hold Until ( IFSCStat == IFS_SCAN_IDLE || IFSCStat == IFS_SCAN_WAIT ) or 4:00;
      Validate Dome_Close;
      > Sun Tracker Sleep
      > Email Report Message Enclosure closed for the evening
  +10 Validate Lights_Out;
}

# Replaces Bedtime_Story
State Up_After_Bedtime {
  > _Algo P1 Up_After_Bedtime
  > Startup Wakeup Clear
  # Validate Pump_Down_Init;
  # Hold until Valid (Pump_Idle);

  # Do Cell scans on Monday
  # Validate Bedtime_Story;

  Validate Init_Choice;
}

# States for Bedtime Stories

State Lights_Out {
  > _Algo P1 Lights_Out
# > IFS Direct SRC=0
# > IFS Set Preamp Gains 3 3
# Hold Until ( (IFSCStat == IFS_SCAN_IDLE) ||
#               (IFSCStat == IFS_SCAN_WAIT) );
  > SaveLog Bedtime Shutdown
  > Startup Wakeup Set
  Validate Quick_Shutdown;
}

###

# Just Make sure IFS gets to idle mode, but leave
# Dome where it is.
State Quick_Shutdown {
  > _Algo P1 Quick_Shutdown
  "Waiting for IFS Idle";
# Hold Until ( IFSCStat == IFS_SCAN_IDLE );
  Validate Shutdown;
}

# Interactive full shutdown
# Make sure IFS gets to idle, then close up
State Full_Shutdown {
  > _Algo P1 Full_Shutdown
  "Waiting for IFS Idle";
# Hold Until ( IFSCStat == IFS_SCAN_IDLE ) OR 1:00;
  Validate Dome_Close;
  "Waiting for Enclosure to Close";
  Hold Until Valid ( Dome_Closed ) or 2:00;
  Validate Shutdown;
}

State Shutdown {
     > _Algo P1 Shutdown
  +1 > Enclosure Exit
  +1 > Sun Tracker Exit
# +1 > IFS Exit
  +1 > Startup Unexpected Clear
  +3 > Quit
}

Partition
#----------------------------------------------
# Partition 2: Watchdog
#----------------------------------------------

State Watchdog_Okay {
  depending on ( Dome_Failed ) { Validate Watchdog_Error; }
  depending on ( WTX_Failed ) { Validate Watchdog_Error; }
  #depending on ( Pump_Failed ) { Validate Watchdog_Error; }
  #depending on ( IdleScan_Failed ) { Validate Watchdog_Error; }
}

State Watchdog_Error {
}
Partition
#----------------------------------------------
# Partition 3: Weather
#----------------------------------------------

State Bad_Weather {
  { if (WTX_drv_stale == 255 || WTX_dev_stale == 255)
      Validate WTX_Failed; } 
  { if ( (convert(WTX_PrecipInt) == 0) &&
         convert(WTX_RH) < RH_Open_Pct /* && ws_low */ &&
         ok_to_open )
      Validate Good_Weather;
  }
  > _Algo P3 Bad_Weather
}

State Good_Weather {
  { if (WTX_drv_stale == 255 || WTX_dev_stale == 255)
      Validate WTX_Failed; } 
  { if (convert(WTX_PrecipInt) > 0 || convert(WTX_RH) > RH_Close_Pct) {
      Weather_Reason = "Rain";
      Validate Bad_Weather;
    }
  }
  { if ( convert(radiance_ratio) < Rad_Close_Pct ) {
      Weather_Reason = "Dark Sky";
      Validate Bad_Weather;
    }
  }
# { if ( ws_high ) {
#     Weather_Reason = "High Winds";
#     Validate Bad_Weather;
#   }
# }
  > _Algo P3 Good_Weather
}

State WTX_Failed {
   { msg (0, "WTX failure. Indicated by WTX stale counter." ); }
   > _Algo P3 WTX_Failed
   > Email Error Message WTX failure.  Indicated by WTX stale counter.
   +1:00 { if (WTX_drv_stale < 2 && WTX_dev_stale <2) Validate Bad_Weather; }
}

Partition
#----------------------------------------------
# Partition 4: Solar Partition
#   Solar_Eval: Figure out which quadrant we're in
#     Solar_Day_Ascending
#     Solar_Day_Descending
#     Solar_Night_Ascending
#     Solar_Night_Descending
#   Solar_Bedtime
#----------------------------------------------
{
  ST_elev_t Sol_ele0; Invalidate Sol_ele0;
  ST_elev_t dEle; Invalidate dEle;
  { depending on (Sol_ele0) {
      dEle = Sol_ele - Sol_ele0;
      Validate dEle;
    }
    Sol_ele0 = Sol_ele;
    Validate Sol_ele0;
  }
}

State Solar_Eval {
      > _Algo P4 Solar_Eval
   +1 {
        if (dEle > 0) {
          if (Sol_ele > 0)
            Validate Solar_Day_Ascending;
          else
            Validate Solar_Night_Ascending;
        } else if (dEle < 0) {
          if (Sol_ele > 0)
            Validate Solar_Day_Descending;
          else
            Validate Solar_Night_Descending;
        }
      }
}

State Solar_Night_Descending {
  > _Algo P4 Solar_Night_Descending
  { if (dEle > 0) {
      /* Midnight */
      Validate Solar_Night_Ascending;
    }
  }
}

State Solar_Night_Ascending {
  > _Algo P4 Solar_Night_Ascending
  { if (Sol_ele > 0) {
      /* Sunrise */
      Validate Solar_Day_Ascending;
    } else if (dEle < 0) {
      /* Midwinter noon: sun reverses direction w/o rising */
      Validate Solar_Bedtime;
    }
  }
}

State Solar_Day_Ascending {
  > _Algo P4 Solar_Day_Ascending
  { if (dEle < 0) {
      /* Noon */
      Validate Solar_Day_Descending;
    }
  }
}

State Solar_Day_Descending {
  > _Algo P4 Solar_Day_Descending
  { if (Sol_ele <= 0) {
      /* Sunset */
      Validate Solar_Bedtime;
    }
  }
}

State Solar_Bedtime {
  > _Algo P4 Solar_Bedtime
}


Partition

#----------------------------------------------
# Partition 5: Enclosure Control
#----------------------------------------------
State Dome_Holding {
}

# STEnc_pos&1 indicates Closed
# STEnc_pos&2 indicates Opened
State Dome_Close {
  { if ( STEnc_stale == 255 ) Validate Dome_Lost_Contact; }
  Hold Until ( !(STEnc_pos&1) ) or 2
  else Validate Dome_Closed;
  > Enclosure Close
  Hold Until (STEnc_pos&1) or 2:00
  else Validate Dome_Close_Failed;
  Validate Dome_Closed;
}

State Dome_Closed {
  { if ( STEnc_stale == 255 ) Validate Dome_Lost_Contact; }
}

State Dome_Open {
  { if ( STEnc_stale == 255 ) Validate Dome_Lost_Contact; }
  Hold Until (!(STEnc_pos&2)) or 2
  else Validate Dome_Opened;
  > Enclosure Open
  Hold Until (STEnc_pos&2) or 2:00
  else Validate Dome_Open_Failed;
  Validate Dome_Opened;
}

State Dome_Opened NoLog {
  { if (STEnc_stale == 255) Validate Dome_Lost_Contact; }
}

State Dome_Close_Failed {
  { msg( 2, "Enclosure failed to close" ); }
  > Email Error Message Enclosure failed to close
  Validate Dome_Failed;
}

State Dome_Open_Failed {
  { msg( 2, "Enclosure failed to open" ); }
  > Email Error Message Enclosure failed to open
  Validate Dome_Failed;
}

State Dome_Lost_Contact {
  { msg( 2, "Lost contact wtih Enclosure or driver" ); }
  > Email Error Message Lost contact with Enclosure or driver
  Validate Dome_Failed;
}

State Dome_Failed {}
